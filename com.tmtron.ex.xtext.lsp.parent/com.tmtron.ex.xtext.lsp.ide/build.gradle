plugins {
	id 'com.github.johnrengelman.shadow' version '2.0.0'
}

dependencies {
	compile project(':com.tmtron.ex.xtext.lsp')
	compile "org.eclipse.xtext:org.eclipse.xtext.ide:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase.ide:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.java:${xtextVersion}"
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
mainClassName = "org.eclipse.xtext.ide.server.ServerLauncher"

shadowJar {
	from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
	configurations = [project.configurations.runtime]
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA','schema/*',
		'.options', '.api_description', '*.profile', '*.html', 'about.*', 'about_files/*',
		'plugin.xml', 'modeling32.png', 'systembundle.properties', 'profile.list')
	classifier = 'ls'
	append('plugin.properties')
}

task explode(type: Copy) {
    final zipFile = file("${buildDir}/distributions/com.tmtron.ex.xtext.lsp.ide-1.0.0-SNAPSHOT.zip")
    final outputDir = file("${buildDir}/lsp/")
 
    from zipTree(zipFile)
    into outputDir
}

build.finalizedBy explode

/* Workaround to avoid batch script errors because the classpath line may be too long
 * "The input line is too long."
 *
 * - https://gist.github.com/jlmelville/2bfe9277e9e2c0ff79b6* 
 * - https://stackoverflow.com/questions/201816/how-to-set-a-long-java-classpath-in-windows/202034#202034
 */
tasks.withType(CreateStartScripts).each { task ->
    task.doLast {
        String text = task.windowsScript.text
        text = text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
        task.windowsScript.write text
    }
}